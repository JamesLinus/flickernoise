imagefiles =
   pacman:	pacman.jpg,
   ghost:	ghost.jpg,
   pleiades:	pleiades.jpg;

decay = 0.6;
image1_a = 0.6;
image2_a = 0.6;

midi "Faderfox LV3" {
	fad1 = fader(1, 7);
	fad8 = fader(8, 7);

	green1 = button(1, 16);
	green2 = button(2, 16);
	green8 = button(8, 16);

	joy1x = pot(9, 1);
	joy1y = pot(9, 2);
	joy2x = pot(10, 1);
	joy2y = pot(10, 2);
};

size_pac = range(fad1);
size_ghost = range(fad8);

pac_ghost = button(green1);
pac_stars = button(green2);
ghost_pac = button(green8);

joy1x = range(joy1x);
joy1y = range(joy1y);
joy2x = range(joy2x);
joy2y = range(joy2y);

per_frame:
	image1_zoom = size_pac*5+0.1;
	image2_zoom = size_ghost*6+0.1;
	image1_index = pac_ghost ? ghost : pac_stars ? pleiades : pacman;
	image2_index = ghost_pac ? pacman : ghost;

	sx = 1.06;
	sy = 1.06;
	cx = 0.5+0.1*sin(1.82*time);
	cy = 0.5+0.1*sin(0.97*time);
	rot = 0.05*sin(0.4*time);

	image1_x = joy1x;
	image1_y = 1-joy1y;
	image2_x = joy2x; 
	image2_y = 1-joy2y;
